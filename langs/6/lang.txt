metadata
  name
    local: Espa√±ol
    1: Spanish
  creator: Natlang
syntax
  start-with: CP
  auto-setlang: AP; CP; IP; NP; PP; DP; VP; conjP; I
  node-types
    AP
      xbar: ~; ~; adjective; ~
    CP
      xbar: ~; ~; complementizer; IP
    IP
      xbar: ~; ~; I; VP
    NP
      xbar: ~; AP?; noun; ~
    PP
      xbar: ~; ~; preposition; DP
    I
      structure: [I tense aspect mood]
    DP
      variable: $head:determiner
      option ($head(hasspec=true))
        xbar: DP; ~; $head; NP
      option ($head(hasspec=false))
        xbar: ~; ~; $head; NP
    VP
      variable: $head:verb
      option ($head(objects=1); $head(object1=NP))
        xbar: DP; adverb?; $head; DP
      option ($head(objects=1); $head(object1=PP))
        xbar: DP; adverb?; $head; PP
      option ($head(objects=1); $head(object1=AP))
        xbar: DP; adverb?; $head; AP
      option ($head(objects=0))
        xbar: DP; adverb?; $head; ~
      option ($head(objects=1); $head(object1=CP))
        xbar: DP; adverb?; $head; CP
lexc
  split-root: _
  noun
    #format: {root[0]}<n>{plural}{case}
    format: {root[0]}<n>{plural}
    tags
      plural: number
        default: <sg>
      case: case
        default: <nom>
    in: NounRoot
    to: NounInfl
  verb
    format: {root[0]}<v>{type}{tense}{person}{number}
    tags
      type
        case (objects=1): <tv>
        case (objects=0): <iv>
        default: <tv>
      tense: tense
        default: <prs>
      person: person
        default: <3>
      number: number
        default: <sg>
    lexicon
      case (objects=1)
        in: VerbRoot
        to: VerbInfl-Transitive
        regex-match: ^(.*)([aei])r$
        regex-replace: \1%{V\2%}
      case (objects=0)
        in: VerbRoot
        to: VerbInfl-Intransitive
        regex-match: ^(.*)([aei])r$
        regex-replace: \1%{V\2%}
  mood
    format: {root[0]}<aux>{tense}{person}{number}
    tags
      tense: tense
        default: <error>
      person: person
        #default: <3>
        default: <7>
      number: number
        default: <sg>
    in: AuxRoot
    to: AuxInfl
  aux
    format: {root[0]}<aux>{tense}{person}{number}
    tags
      tense: tense
        default: <error>
      person: person
        default: <3>
      number: number
        default: <sg>
    in: AuxRoot
    to: AuxInfl
  adjective
    bland: <adj>
  determiner
    bland: <det>
  pronoun
    bland: <prn>
  preposition
    bland: <prep>
  conjunction
    bland: <conj>
  adverb
    bland: <adv>
  quantifier
    bland: <quant>
  complementizer
    bland: <comp>
  rel-pronoun
    bland: <prn>
  contraction
    bland: <c't>
transform
#TAM + agreement
  rule
    form: [Ibar [I $tense? $aspect? $mood] |[VP $subj $mod? $head $comp?]]
    result: [Ibar [I $tense $aspect ~] |[VP $subj $mood |[VP ~ $mod $head $comp]]]
    setprop ($head.tense): $mood.conj
  rule
    context: [Ibar [I $tense $aspect? ~] |[?VP @]]
    form: $head
    setprop (tense): $tense.tense
  rule
    context: |[VP |[DP $ds? $dm? $dh(is-rel=false) $dc?] $vm? @ $vc?]
    form: $verb
    setprop (number): $dh.number
    setprop (person): $dh.person
  rule
    context: |[?DP $dh |[?NP $nh? |[?CP $ch? |[?IP $ih? |[?VP |[?DP $s:rel-pronoun] @ $vc?]]]]]
    form: $verb
    setprop (person): $dh.person
    setprop (number): $dh.number
#plurality
  rule
    context: [Dbar $head |[?NP @]]
    form: $noun
    setprop (number): $head.number
#relative clauses
  rule
    form: |[IP ~ $imod? $i? [VP |[DP $subj:rel-pronoun] $rest?]]
    result: |[IP |[DP $subj] $imod $i [VP ~ $rest]]
  rule
    form: |[IP ~ $imod? $i? |[VP $subj? $vmod? $v? |[DP $obj:rel-pronoun]]]
    result: |[IP |[DP $obj] $imod? $i? |[VP $subj? $vmod? $v? ~]]
  rule
    context: [Nbar $noun:noun @]
    form: |[CP ~ $cmod? complementizer=-Q [IP |[DP $rel:rel-pronoun] $rest]]
    result: |[CP |[DP $rel] $cmod complementizer=-Q [IP ~ $rest]]
  rule
    context: [Nbar $noun:noun @]
    form: |[CP ~ $cmod? complementizer=-Q |[conjP [IP |[DP $rel:rel-pronoun] $rest] $jmod? $j? $jbar?]]
    result: |[CP |[DP $rel] $cmod complementizer=-Q |[conjP [IP ~ $rest] $jmod $j $jbar]]
#subject position
  rule
    form: |[IP ~ $imod? $ihead |[VP $subj $mod? $head $comp?]]
    result: |[IP $subj $imod $ihead |[VP ~ $mod $head $comp]]

#rotations
  rotate: Vmod
  rotate: Nmod
