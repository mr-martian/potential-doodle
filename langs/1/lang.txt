metadata
  name
    local: English
  creator: Natlang
syntax
  start-with: CP
  auto-setlang: AP; CP; IP; NP; PP; DP; VP
  node-types
    AP
      xbar: ~; ~; adjective; ~
    CP
      xbar: ~; ~; complementizer; IP
    IP
      xbar: ~; ~; I; VP
    NP
      xbar: ~; AP?; noun; ~
    PP
      xbar: ~; ~; preposition; DP
    I
      variable: $t:tense
      variable: $a:aspect
      variable: $m:mood?
      structure: [I $t $a $m]
      translation (2): [I $t $a $m]
    DP
      variable: $head:determiner
      option ($head(hasspec=true))
        xbar: DP; ~; $head; NP
      option ($head(hasspec=false))
        xbar: ~; ~; $head; NP
    VP
      variable: $head:verb
      option ($head(objects=1); $head(object1=NP))
        xbar: DP; adverb?; $head; DP
      option ($head(objects=1); $head(object1=PP))
        xbar: DP; adverb?; $head; PP
      option ($head(objects=1); $head(object1=AP))
        xbar: DP; adverb?; $head; AP
      option ($head(objects=0))
        xbar: DP; adverb?; $head; ~
      option ($head(objects=1); $head(object1=CP))
        xbar: DP; adverb?; $head; CP
tag-order
  split-root: _
  noun
    format: {root[0]}<n>{plural}{case}
    tags
      plural: number
        default: <SG>
      case: case
        default: <nom>
  verb
    format: {root[0]}<v>{type}{tense}{person}{number}
    tags
      type
        case (objects=1): <tv>
        case (objects=0): <iv>
        default: <tv>
      tense: tense
        default: <nonpst>
      person: person
        default: <3>
      number: number
        default: <SG>
  mood
    format: {root[0]}<aux>{tense}
    tags
      tense: tense
        default: <error>
  adjective
    format: {root[0]}<adj>
  determiner
    format: {root[0]}<det>
lexicon-generation
  noun
    in: NounRoot
    to: NounInfl
  verb (objects=1)
    in: VerbRoot
    to: VerbInfl-Transitive
  verb (objects=0)
    in: VerbRoot
    to: VerbInfl-Intransitive
  mood
    in: AuxRoot
    to: AuxInfl
  adjective
    in: AdjRoot
    to: AdjInfl
  determiner
    in: DeterminerRoot
    to: DeterminerInfl
transform
#TAM + agreement
  rule
    form: [Ibar [I $tense aspect=perfect-continuous $mood?] |[VP $subj $mod? $head $comp?]]
    result: [Ibar [I $tense ~ $mood] |[VP $subj ~ aux=have |[VP ~ ~ aux=be |[VP ~ $mod $head $comp]]]]
  rule
    form: [Ibar [I $tense aspect=continuous $mood?] |[VP $subj $mod? $head $comp?]]
    result: [Ibar [I $tense ~ $mood] |[VP $subj ~ aux=be |[VP ~ $mod $head $comp]]]
  rule
    form: [Ibar [I $tense aspect=perfect $mood?] |[VP $subj $mod? $head $comp?]]
    result: [Ibar [I $tense ~ $mood] |[VP $subj ~ aux=have |[VP ~ $mod $head $comp]]]
  rule
    context: |[VP $vs? $vm? $aux:aux |[VP $vs2? $vm2? @ $vc?]]
    form: $verb:verb
    setprop (tense): $aux.conj
  rule
    context: |[CP * * complementizer=+Q [IP ~ [Imod * @]]]
    form: [Ibar [I $tense ~ $mood] |[VP $subj $mod $head $comp]]
    result: [Ibar [I $tense ~ ~] |[VP $subj ~ $mood |[VP ~ $mod $head $comp]]]
  rule
    context: |[CP * * complementizer=+Q [IP ~ [Imod * @]]]
    form: [Ibar [I $tense aspect=durative ~] |[VP $subj $mod $head:verb $comp]]
    result: [Ibar [I $tense aspect=durative ~] |[VP $subj ~ aux=do |[VP ~ $mod $head $comp]]]
  rule
    form: [Ibar [I $t? $a? $mood] |[VP $subj $mod? $head $comp?]]
    result: [Ibar [I $t $a ~] |[VP $subj ~ $mood |[VP ~ $mod $head $comp]]]
  rule
    context: [Ibar [I $tense $aspect? $mood?] |[VP $subj $mod? @ $comp?]]
    form: $head
    setprop (tense): $tense.tense
  rule
    context: |[VP |[DP $ds? $dm? $dh $dc?] $vm? @ $vc?]
    form: $verb:verb
    setprop (number): $dh.number
  rule
    context: |[VP |[DP $ds? $dm? $dh $dc?] $vm? @ $vc?]
    form: $verb:verb
    setprop (person): $dh.person
  rule
    context: |[CP * * complementizer=+Q @]
    form: |[IP ~ $mod $I |[VP $subj $vmod $head:aux $comp]]
    result: |[IP $head $mod $I |[VP $subj $vmod ~ $comp]]
#relative clauses
  rule
    form: |[IP ~ $imod? $i? [VP |[DP $subj:rel-pronoun] $rest?]]
    result: |[IP |[DP $subj] $imod $i [VP ~ $rest]]
  rule
    form: |[IP ~ $imod? $i? |[VP $subj? $vmod? $v? |[DP $obj:rel-pronoun]]]
    result: |[IP |[DP $obj] $imod? $i? |[VP $subj? $vmod? $v? ~]]
  rule
    context: [Nbar $noun:noun @]
    form: |[CP ~ $cmod? complementizer=-Q [IP |[DP $rel:rel-pronoun] $rest]]
    result: |[CP |[DP $rel] $cmod complementizer=-Q [IP ~ $rest]]
  rule
    context: [Nbar $noun:noun @]
    form: |[CP ~ $cmod? complementizer=-Q |[conjP [IP |[DP $rel:rel-pronoun] $rest] $jmod? $j? $jbar?]]
    result: |[CP |[DP $rel] $cmod complementizer=-Q |[conjP [IP ~ $rest] $jmod $j $jbar]]
#plurality
  rule
    context: [Dbar $head |[NP $s? $m? @ $c?]]
    form: $noun
    setprop (number): $head.number
#possession
  rule
    form: |[DP $spec:DP $mod? $head $comp?]
    result: |[DP <suffix $spec $head> $mod ~ $comp]
#subject position
  rule
    form: |[IP ~ $imod? $ihead |[VP $subj $mod? $head $comp?]]
    result: |[IP $subj $imod $ihead |[VP ~ $mod $head $comp]]
#rotations
  rotate: Vmod
