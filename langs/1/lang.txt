metadata
  name
    local: English
  creator: Natlang
syntax
  start-with: CP
  auto-setlang: AP; CP; IP; NP; PP; DP; VP; conjP; I
  node-types
    AP
      xbar: ~; ~; adjective; ~
    CP
      xbar: ~; ~; complementizer; IP
    IP
      xbar: ~; ~; I; VP
    NP
      xbar: ~; AP?; noun; ~
    PP
      xbar: ~; ~; preposition; DP
    I
      structure: [I $t:tense $a:aspect $m:mood?]
    DP
      variable: $head:determiner
      option ($head(hasspec=true))
        xbar: DP; ~; $head; NP
      option ($head(hasspec=false))
        xbar: ~; ~; $head; NP
    VP
      variable: $head:verb
      option ($head(objects=1); $head(object1=NP))
        xbar: DP; adverb?; $head; DP
      option ($head(objects=1); $head(object1=PP))
        xbar: DP; adverb?; $head; PP
      option ($head(objects=1); $head(object1=AP))
        xbar: DP; adverb?; $head; AP
      option ($head(objects=0))
        xbar: DP; adverb?; $head; ~
      option ($head(objects=1); $head(object1=CP))
        xbar: DP; adverb?; $head; CP
lexc: hfst
  split-root: _
  noun
    format: {root[0]}<n>{plural}{case}
    tags
      plural: number
        default: <SG>
      case: case
        default: <nom>
    in: NounRoot
    to: NounInfl
  verb
    format: {root[0]}<v>{type}{tense}{person}{number}
    tags
      type
        case (objects=1): <tv>
        case (objects=0): <iv>
        default: <tv>
      tense: tense
        default: <nonpst>
      person: person
        default: <3>
      number: number
        default: <SG>
    lexicon
      case (objects=1)
        in: VerbRoot
        to: VerbInfl-Transitive
      case (objects=0)
        in: VerbRoot
        to: VerbInfl-Intransitive
  mood
    format: {root[0]}<aux>{tense}{person}{number}
    tags
      tense: tense
        default: <error>
      person: person
        default: <3>
      number: number
        default: <SG>
    in: AuxRoot
    to: AuxInfl
  aux
    format: {root[0]}<aux>{tense}{person}{number}
    tags
      tense: tense
        default: <error>
      person: person
        default: <3>
      number: number
        default: <SG>
    in: AuxRoot
    to: AuxInfl
  adjective
    bland: <adj>
  determiner
    bland: <det>
  pronoun
    bland: <prn>
  preposition
    bland: <prep>
  conjunction
    bland: <conj>
  adverb
    bland: <adv>
  quantifier
    bland: <quant>
  complementizer
    bland: <comp>
  rel-pronoun
    bland: <prn>
transform
#TAM + agreement
  rule
    form: [Ibar [I $tense aspect=perfect-continuous $mood?] |[VP $subj $mod? $head $comp?]]
    result: [Ibar [I $tense ~ $mood] |[VP $subj ~ aux=have |[VP ~ ~ aux=be |[VP ~ $mod $head $comp]]]]
  rule
    form: [Ibar [I $tense aspect=continuous $mood?] |[VP $subj $mod? $head $comp?]]
    result: [Ibar [I $tense ~ $mood] |[VP $subj ~ aux=be |[VP ~ $mod $head $comp]]]
  rule
    form: [Ibar [I $tense aspect=perfect $mood?] |[VP $subj $mod? $head $comp?]]
    result: [Ibar [I $tense ~ $mood] |[VP $subj ~ aux=have |[VP ~ $mod $head $comp]]]
  rule
    form: [Ibar [I $tense? $aspect? $mood] |[VP $subj $mod? $head $comp?]]
    result: [Ibar [I $tense $aspect ~] |[VP $subj $mood |[VP ~ $mod $head $comp]]]
    #setprop ($head.tense): $mood.conj
  #rule
  #  context: [Ibar [I $tense? $aspect? $mood] |[?VP @]]
  #  form: $verb
  #  setprop (tense): $mood.conj
  rule
    context: |[CP * * complementizer=+Q [IP ~ [Imod * @]]]
    form: [Ibar [I $tense? aspect=durative ~] |[VP $subj $mod? $head $comp?]]
    result: [Ibar [I $tense aspect=durative ~] |[VP $subj ~ aux=do |[VP ~ $mod $head $comp]]]
  rule
    context: [Vbar $aux |[?VP @]]
    form: $verb
    setprop (tense): $aux.conj
  rule
    context: [Vbar $aux:mood |[?VP @]]
    form: $verb
    setprop (tense): $aux.conj
  #rule
  #  context: |[CP * * complementizer=+Q [IP ~ [Imod * @]]]
  #  form: [Ibar [I $tense ~ $mood] |[VP $subj $mod $head $comp]]
  #  result: [Ibar [I $tense ~ ~] |[VP $subj ~ $mood |[VP ~ $mod $head $comp]]]
  #rule
  #  form: [Ibar [I $t? $a? $mood] |[VP $subj $mod? $head $comp?]]
  #  result: [Ibar [I $t $a ~] |[VP $subj ~ $mood |[VP ~ $mod $head $comp]]]
  rule
    context: [Ibar [I $tense $aspect? ~] |[?VP @]]
    form: $head
    setprop (tense): $tense.tense
  rule
    context: |[VP |[DP $ds? $dm? $dh(is-rel=false) $dc?] $vm? @ $vc?]
    form: $verb
    setprop (number): $dh.number
    setprop (person): $dh.person
  rule
    context: |[?DP $dh |[?NP $nh? |[?CP $ch? |[?IP $ih? |[?VP |[?DP $s:rel-pronoun] @ $vc?]]]]]
    form: $verb
    setprop (person): $dh.person
    setprop (number): $dh.number
  rule
    context: |[CP * * complementizer=+Q @]
    form: |[IP ~ $mod $I |[VP $subj $vmod $head:aux $comp]]
    result: |[IP $head $mod $I |[VP $subj $vmod ~ $comp]]
#relative clauses
  rule
    form: |[IP ~ $imod? $i? [VP |[DP $subj:rel-pronoun] $rest?]]
    result: |[IP |[DP $subj] $imod $i [VP ~ $rest]]
  rule
    form: |[IP ~ $imod? $i? |[VP $subj? $vmod? $v? |[DP $obj:rel-pronoun]]]
    result: |[IP |[DP $obj] $imod? $i? |[VP $subj? $vmod? $v? ~]]
  rule
    context: [Nbar $noun:noun @]
    form: |[CP ~ $cmod? complementizer=-Q [IP |[DP $rel:rel-pronoun] $rest]]
    result: |[CP |[DP $rel] $cmod complementizer=-Q [IP ~ $rest]]
  rule
    context: [Nbar $noun:noun @]
    form: |[CP ~ $cmod? complementizer=-Q |[conjP [IP |[DP $rel:rel-pronoun] $rest] $jmod? $j? $jbar?]]
    result: |[CP |[DP $rel] $cmod complementizer=-Q |[conjP [IP ~ $rest] $jmod $j $jbar]]
#plurality
  rule
    context: [Dbar $head |[?NP @]]
    form: $noun
    setprop (number): $head.number
#possession
  rule
    form: |[DP $spec:DP $mod? $head $comp?]
    result: |[DP [GEN $spec $head] $mod ~ $comp]
  rule
    #form: [GEN |[?DP $d? |[?NP $noun ~]] $gen]
    #result: |[$DP $d |[$NP $noun ~]]
    form: [GEN |[DP $ds? $dm? $dh? |[NP $ns? $nm? $noun ~]] $gen]
    result: |[DP $ds $dm $dh |[NP $ns $nm $noun ~ ]]
    setprop ($noun.case): gen
#subject position
  rule
    form: |[IP ~ $imod? $ihead |[VP $subj $mod? $head $comp?]]
    result: |[IP $subj $imod $ihead |[VP ~ $mod $head $comp]]
#conjunction-modifier order
  rule
    form: |[conjP $s? $m? $h? $c?]
    result: |[conjP $s $h $m $c]
#rotations
  rotate: Vmod
