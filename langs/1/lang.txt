metadata
  name
    local: English
  creator: Natlang
syntax
  start-with: CP
  auto-setlang: AP; CP; IP; NP; PP; DP; VP; conjP; I
  node-types
    AP
      xbar: ~; ~; adjective; ~
      require: adjective
    CP
      xbar: ~; ~; complementizer; IP
    IP
      xbar: ~; ~; I; VP
    NP
      xbar: ~; AP?; noun; CP?
      require: noun
    PP
      xbar: ~; ~; preposition; DP
      require: preposition
    I
      structure: [I $t:tense $a:aspect? $m:mood?]
    DP
      variable: $head:determiner?
      option ($head(hasspec=true))
        xbar: DP; ~; $head; NP
        require: determiner
      option ($head(hasspec=false))
        xbar: ~; ~; $head; NP
        require: determiner
      option ($head!)
        xbar: ~; ~; $pro:pronoun; ~
        require: pronoun
    VP
      variable: $head:verb
      option ($head(objects=1); $head(object1=NP))
        xbar: DP; adverb?; $head; DP
        require: verb
      option ($head(objects=1); $head(object1=PP))
        xbar: DP; adverb?; $head; PP
        require: verb
      option ($head(objects=1); $head(object1=AP))
        xbar: DP; adverb?; $head; AP
        require: verb
      option ($head(objects=1); $head(object1=NP/AP))
        xbar: DP; adverb?; $head; DP
        require: verb
      option ($head(objects=1); $head(object1=NP/AP))
        xbar: DP; adverb?; $head; AP
        require: verb
      option ($head(objects=0))
        xbar: DP; adverb?; $head; ~
        require: verb
      option ($head(objects=1); $head(object1=CP))
        xbar: DP; adverb?; $head; CP
        require: verb
      option
        structure: |[VP $head]
        #default to intransitive if unspecified
lexc: hfst
  split-root: _
  noun
    format: {root[0]}<n>{plural}{case}
    tags
      plural: number
        default: <SG>
      case: case
        default: <nom>
    in: NounRoot
    to: NounInfl
  verb
    format: {root[0]}<v>{tense}{person}{number}
    tags
      tense: tense
        default: <nonpst>
      person: person
        default: <3>
      number: number
        default: <SG>
    in: VerbRoot
    to: VerbInfl
  mood
    format: {root[0]}<aux>{tense}{person}{number}
    tags
      tense: tense
        default: <error>
      person: person
        default: <3>
      number: number
        default: <SG>
    in: AuxRoot
    to: AuxInfl
  aux
    format: {root[0]}<aux>{tense}{person}{number}
    tags
      tense: tense
        default: <error>
      person: person
        default: <3>
      number: number
        default: <SG>
    in: AuxRoot
    to: AuxInfl
  aspect
    format: {root[0]}<aux>{tense}{person}{number}
    tags
      tense: tense
        default: <error>
      person: person
        default: <3>
      number: number
        default: <SG>
    in: AuxRoot
    to: AuxInfl
  adjective
    bland: <adj>
  determiner
    bland: <det>
  pronoun
    bland: <prn>
  preposition
    bland: <prep>
  conjunction
    bland: <conj>
  adverb
    bland: <adv>
  quantifier
    bland: <quant>
  complementizer
    bland: <comp>
  rel-pronoun
    bland: <prn>
transform
  rule: do insertion
    context: |[*CP complementizer=+Q |[*IP @]]
    form: <I $tense?>
    result: [I $tense aux=do]
  multirule: gather person/number
    layer?: |[DP $spec? $mod? $notrel |[NP $nspec? $nmod? $noun @]]
      result: |[DP $spec? $mod? $notrel |[NP $nspec? $nmod? $noun @]]
    layer?
      form: |[conjP |[CP $CPspec? $CPmod? $comp? @] $conj? $ccomp?]
        result: |[conjP |[CP $CPspec $CPmod $comp @] $conj $ccomp]
      form: |[CP $CPspec? $CPmod? $comp? @]
        result: |[CP $CPspec $CPmod $comp @]
    layer: |[IP <I $tense $aspect? $mood?> @]
      result: |[IP <I $tense $aspect? $mood?> @]
      cond
        option ($notrel)
          setprop ($tense.person): $notrel.person
          setprop ($tense.number): $notrel.number
        option
          setprop ($tense.person): $subj.person
          setprop ($tense.number): $subj.number
    layer
      form: |[vP @ $m? $h? $c?]
        result: |[vP @ $m $h $c]
      form: |[VP @ $m? $h? $c?]
        result: |[VP @ $m $h $c]
    layer?: |[conjP @ $conj2? $ccomp2?]
      result: |[conjP @ $conj2? $ccomp2?]
    layer: |[?DP $subj]
      result: |[$DP $subj]
  multirule: agreement and subject position
    layer
      form: |[IP <I $tense:tense $aspect:aspect? $mood:mood?> @]
        result: |[IP $subj <I $tense $mood? $aspect?> @]
        distribute (conj; tense): $tense; $mood; $aspect; $verb
        distribute (person; person): $tense; $mood; $aspect; $verb
        distribute (number; number): $tense; $mood; $aspect; $verb
    layer
      form: |[?vP $subj? ~ |[?VP $verb]]
        result: |[$vP ~ $verb |[$VP ~]]
      form: |[?VP $subj? $verb $c?]
        result: |[$VP ~ $verb $c]
  rule: aux-inversion
    form: |[CP ~ $mod? complementizer=+Q |[?IP <I $tense? $first $other+?>]]
    result: |[CP $first $mod complementizer=+Q |[$IP <I $tense $other+>]]
  multirule: relative pronoun position
    layer: |[NP $NPspec? $NPmod? $NPhead? @]
      result: |[NP $NPspec $NPmod $NPhead @]
    layer?: |[conjP @ $conj? $conjcomp?]
      result: |[conjP @ $conj $conjcomp]
    layer: |[CP ~ $CPmod? ~ @]
      result: |[CP ~ $CPmod |[$DP $rel] @]
    layer
      form: |[IP |[?DP $rel:rel-pronoun] $m? $h? $c?]
        result: |[IP ~ $m $h $c]
      form: |[IP $is? $im? $ih? |[VP $vs? $vm? $vh? |[?DP $rel:rel-pronoun]]]
        result: |[IP $is $im $ih |[VP $vs $vm $vh ~]]
  rule: plural marking
    form: |[?DP $head |[?NP $noun]]
    setprop ($noun.number): $head.number
  rule: attach genitive suffix
    form: |[DP $spec:DP $mod? $head $comp?]
    result: |[DP [GEN $spec $head] $mod ~ $comp]
  rule: switch to case marking, if applicable
    form: [GEN |[?DP $dh? |[?NP $noun ~]] $gen]
    result: |[?DP $dh |[?NP $noun ~]]
    setval ($noun.case): gen
  rotate: Vmod
  rotate: Nmod
  rule
    form: [Dmod $m:PP $b]
    rotate
